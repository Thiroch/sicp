# Code
```scheme
#lang sicp

(define (square x)
  (* x x))
(define (average x y)
  (/ (+ x y) 2))

(define (sqrt x)
  (define (improve guess x)
    (average guess (/ x guess)))
  (define (sqrt-iter guess x)
    (display guess) (display "\n") ; display current guess
    (if (good-enough? guess x)
        guess
        (sqrt-iter (improve guess x) x)))

  (define (good-enough? guess x)
    (< (abs (- (square guess) x)) 0.001))

  (sqrt-iter 1.0 x))
```

# What `good-enough?` does
The `good-enough?` does the following
1. Evaluate the difference between the square of the `guess` and the `x` (the value trying to get the square root of)
2. If the result is less than 0.001, return true.
   Otherwise, return false.
## Problem with small numbers
With the small numbers, if the actual square root is smaller than 0.001, good-enough? wrongly determines that the guess is good enough before it reaches sufficient accuracy.
### Example
When you try to calculate the square root of $0.0001$...
1. `(good-enough? 1.0 0.0001)`
	* Returns false since $\lvert1^2-0.0001\rvert > 0.001$
	* `(improve 1.0 0.0001)` returns $0.50005$
2. `(good-enough? 0.50005 0.0001)`
	* Returns false since $\lvert0.50005^2-0.0001\rvert > 0.001$
	* `(improve 0.50005 0.0001)` returns $0.2501249900009999$
3. `(good-enough? 0.2501249900009999 0.0001)`
	* Returns false since $\lvert0.2501249900009999^2-0.0001\rvert > 0.001$
	* `(improve 0.2501249900009999 0.0001)` returns $0.12526239505846617$
4. `(good-enough? 0.12526239505846617 0.0001)`
	* Returns false since $\lvert0.12526239505846617^2-0.0001\rvert > 0.001$
	* `(improve 0.12526239505846617 0.0001)` returns $0.06303035962394365$
5. `(good-enough? 0.06303035962394365 0.0001)`
	* Returns false since $\lvert0.06303035962394365^2-0.0001\rvert > 0.001$
	* `(improve 0.06303035962394365 0.0001)` returns $0.03230844833048122$
6. `(good-enough? 0.06303035962394365 0.0001)`
	* Returns true since $\lvert0.03230844833048122^2-0.0001\rvert < 0.001$

Thus, the result is $0.03230844833048122$ while the expected result is $0.01$.
## Problem with large numbers
With the large numbers, the limits of floating-point precision would be the problem. At the certain point, the result of the `improve` returns the same value as the previous guess due to the limits of floating-point precision.
### Example
When you try to calculate the square root of $10000000000000$...
1. `(good-enough? 1.0 10000000000000)`
	* Returns false since $\lvert1^2-10000000000000\rvert > 0.001$
	* `(improve 1.0 10000000000000)` returns $5000000000000.5$
2. `(good-enough? 5000000000000.5 10000000000000)`
	* Returns false since $\lvert5000000000000.5^2-10000000000000\rvert > 0.001$
	* `(improve 5000000000000.5 10000000000000)` returns $2500000000001.25$

...

30. `(good-enough? 3162277.6601683795 10000000000000)`
	* Returns false since $\lvert3162277.6601683795^2-10000000000000\rvert > 0.001$
	* `(improve 3162277.6601683795 10000000000000)` returns $3162277.6601683795$
31. `(good-enough? 3162277.6601683795 10000000000000)`
	* Returns false since $\lvert3162277.6601683795^2-10000000000000\rvert > 0.001$
	* `(improve 3162277.6601683795 10000000000000)` returns $3162277.6601683795$

...

Since the `improve` continues to return the same value, the procedure falls into an infinite loop.
# An alternative strategy
As stated in the book
> An alternative strategy for implementing good-enough? is to watch how `guess` changes from one iteration to the next and to stop when the change is a very small fraction of the guess.

```scheme
#lang sicp

(define (square x)
  (* x x))
(define (average x y)
  (/ (+ x y) 2))

(define (improved-sqrt x)
  (define (improve guess x)
    (average guess (/ x guess)))
  (define (sqrt-iter guess x)
    (display guess) (display "\n") ; display current guess
    (define new-guess (improve guess x))
    (if (good-enough? guess new-guess)
        guess
        (sqrt-iter new-guess x)))

  (define (good-enough? guess new-guess)
    (< (abs (- guess new-guess)) (* guess 0.001)))

  (sqrt-iter 1.0 x))
```
Now the `good-enough?` calculates the difference between the current guess and the new guess, and if the difference is a very small fraction of the current guess, then it determines that the guess is sufficiently accurate and returns the current guess as the result.