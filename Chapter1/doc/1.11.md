$$
\begin{cases}
f(n)=n&\text{if }n<3\\
f(n)=f(n-1)+2f(n-2)+3f(n-3)&\text{if }n\ge3
\end{cases}
$$
# Recursive process
## Code
```
(define (f n)
  (cond ((< n 3) n)
        (else (+ (f (- n 1))
                 (* 2 (f (- n 2)))
                 (* 3 (f (- n 3)))))))
```
## The process of evaluating $f(5)$


```
(f 5) = 25
  |_________________________________________________________________________________________________
  |                                                   |                                            |
(f 4)                                            (* 2 (f 3))                                  (* 3 (f 2))
  |________________________________________           |________________________________            |
  |                           |           |           |             |                 |         (* 3 2)
(f 3)                     (*2 (f 2)) (* 3 (f 1)) (* 2 (f 2)) (* 2 (* 2 (f 1)) (* 2 (* 3 (f 0)))    |
  |_____________________      |           |           |             |                 |            6
  |        |           |   (* 2 2)     (* 3 1)        4       (* 2 (* 2 1))     (* 2 (* 3 0))
(f 2) (* 2 (f 1)) (* 3 (f 0)) |           |                         |                 |
  |        |           |      4           3                         4                 0
  2     (* 2 1)     (* 3 0)
           |           |
           2           0
```
# Iterative process
## Code
```
(define (f-iterative n)
  (define (f f1 f2 f3) (+ f1 (* f2 2) (* f3 3)))
  (define (f-iter f1 f2 f3 m)
    (cond ((= n m) (f f1 f2 f3))
          (else (f-iter (f f1 f2 f3) f1 f2 (+ m 1)))))
  (cond ((< n 3) n)
        (else (f-iter 2 1 0 3))))
```
## The process of evaluating $f(5)$
```
(f-iterative 5)
(f-iter 2 1 0 3)
(f-iter 4 2 1 4)
(f-iter 11 4 2 5)
(+ 11 (* 4 2) (* 2 3))
25
```